apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "1.1.2"
apply plugin: 'com.android.library'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 112
        versionName "1.1.2"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compileOnly 'com.android.support:appcompat-v7:28.0.0'
    implementation ('com.squareup.leakcanary:leakcanary-android:1.5.1'){
        exclude group: 'com.android.support'
    }
    implementation 'com.github.markzhai:blockcanary-android:1.5.0'
    implementation  'fairy.easy.crashcanary:crashcanary:1.1.1'
    implementation  'fairy.easy.logcanary:logcanary:1.0.2'
    implementation  'fairy.easy.httpcanary:httpcanary:1.0.5'
}

def siteUrl = 'https://github.com/guxiaonian/FairyLand'
def gitUrl = 'https://github.com/guxiaonian/FairyLand.git'
group = "fairy.easy.land"
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mobile'
                        name 'mobile'
                        email 'gunaonian@geetest.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant2 ->
        variant2.getJavaCompiler().classpath.files
    })
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user =  properties.getProperty("bintray.user")
    key =  properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "FairyLand"
        name = "FairyLandNormal"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}